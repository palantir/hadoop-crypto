buildscript {
    repositories {
        maven { url  "http://palantir.bintray.com/releases" }
        jcenter()
    }

    dependencies {
        classpath 'gradle.plugin.com.palantir:gradle-circle-style:1.1.2'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath 'com.netflix.nebula:nebula-dependency-recommender:5.2.0'
        classpath 'com.netflix.nebula:nebula-publishing-plugin:5.1.4'
        classpath 'com.palantir.baseline:gradle-baseline-java:0.20.1'
    }
}

plugins {
    id 'com.palantir.configuration-resolver' version '0.2.0'
    id 'com.palantir.git-version' version '0.2.0'
    id 'org.inferred.processors' version '1.2.2'
}

repositories {
    // needed for baselineUpdateConfig task
    maven { url  "http://palantir.bintray.com/releases" }
}

apply plugin: 'com.palantir.baseline-config' // provides baselineUpdateConfig task
apply plugin: 'com.palantir.baseline-idea'
apply plugin: 'com.palantir.circle.style'

dependencies {
    baseline 'com.palantir.baseline:gradle-baseline-java-config:0.20.1@zip'
}

allprojects {
    group 'com.palantir.hadoop-crypto2'
    version gitVersion()

    apply plugin: 'com.palantir.configuration-resolver'
    apply plugin: 'nebula.dependency-recommender'

    dependencyRecommendations {
        strategy OverrideTransitives
        propertiesFile file: project.rootProject.file('versions.props')
    }

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.palantir.baseline-checkstyle'
    apply plugin: 'com.palantir.baseline-eclipse'
    apply plugin: 'com.palantir.baseline-idea'
    apply plugin: 'org.inferred.processors'

    repositories {
        maven { url  "http://palantir.bintray.com/releases" }
        jcenter()
    }

    tasks.check.dependsOn(javadoc)

    sourceCompatibility = 1.8

    if (System.env.CIRCLE_TEST_REPORTS) {
        test.reports.junitXml.destination = new File(System.env.CIRCLE_TEST_REPORTS, it.getName())
    }

    // The IBM JVM doesn't play nicely with error-prone
    // This configuration difference forces us to run gradle with --no-daemon
    if (!System.env.DISABLE_ERROR_PRONE) {
        apply plugin: 'com.palantir.baseline-error-prone'
        configurations.errorprone {
            resolutionStrategy {
                force 'com.google.guava:guava:21.0'
            }
        }
    }
}
