apply from: "${rootDir}/gradle/openssl.gradle"
apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
    api project(":crypto-core")
    api "org.apache.hadoop:hadoop-client-api"
    api "org.slf4j:slf4j-api"

    runtimeOnly "org.apache.hadoop:hadoop-client-runtime"

    testImplementation "junit:junit"
    testImplementation "org.assertj:assertj-core"
    testImplementation "org.mockito:mockito-core"
}

shadowJar {
    mergeServiceFiles()

    dependencies {
        // The shadow jar is expected to be added to the Hadoop cli classpath
        // which comes with these deps
        exclude(dependency('org.apache.hadoop:hadoop-client-api'))
        exclude(dependency('org.apache.hadoop:hadoop-client-runtime'))
    }
}

// Automatically shadow all included dependencies
// https://imperceptiblethoughts.com/shadow/configuration/relocation/#automatically-relocating-dependencies
import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "hadoop.crypto.shaded" // Default value is "shadow"
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar
